import dev._100media.gradleutils.GradleUtils

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group 'dev._100media.gradleutils'
version gradleutils.getTagOffsetVersion()
println 'Version: ' + project.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // implementation 'org.codehaus.groovy:groovy-all:3.0.5'
}

task copyGradleUtilsGroovy(type: Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/groovy'
    from('buildSrc/src/main/groovy') {
        include '**/*.groovy'
    }
}

compileGroovy.dependsOn(copyGradleUtilsGroovy)

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

gradlePlugin {
    plugins {
        hundredMediaGradleUtils {
            id = 'dev._100media.gradleutils'
            implementationClass = 'dev._100media.gradleutils.GradleUtilsPlugin'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            // from components.java is already declared by java-gradle-plugin
            artifact sourcesJar
        }
    }
    repositories {
        maven GradleUtils.getPublishing100MediaMaven(project)
    }
}