import dev._100media.gradleutils.GradleUtils

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'dev.gradleplugins.gradle-plugin-development' version '1.6.10'
}

group 'dev._100media.gradleutils'
println 'Version: ' + project.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi('7.5.1')
}

tasks.register('copyGradleUtilsGroovy', Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/groovy'
    from('buildSrc/src/main/groovy') {
        include '**/*.groovy'
    }
}

compileGroovy.dependsOn(copyGradleUtilsGroovy)

tasks.register('sourcesJar', Jar) {
    dependsOn 'classes'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

gradlePlugin {
    plugins {
        hundredMediaGradleUtils {
            id = 'dev._100media.gradleutils'
            implementationClass = 'dev._100media.gradleutils.GradleUtilsPlugin'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            // from components.java is already declared by java-gradle-plugin
            artifact sourcesJar
        }
    }
    repositories {
        maven GradleUtils.getPublishing100MediaMaven(project)
    }
}